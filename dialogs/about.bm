'FTP Client
'Copyright Matt Kilgore -- 2011/2013

'This program is free software, without any warranty of any kind.
'You are free to edit, copy, modify, and redistribute it under the terms
'of the Do What You Want Public License, Version 1, as published by Matt Kilgore
'See file COPYING that should have been included with this source.

SUB about_dialog () 'Displays 'about' box

gui_num = 4
DIM gui(gui_num) AS GUI_element_type

gui(1).element_type = GUI_BOX
gui(1).dialog = -1
GUI_init_element gui(1), "About"
gui(1).row1 = _HEIGHT(0) \ 2 - 3
gui(1).row2 = gui(1).row1 + 7
gui(1).col1 = _WIDTH(0) \ 2 - 20
gui(1).col2 = _WIDTH(0) \ 2 + 20
gui(1).shadow = -1
gui(1).layer = -1 'Set in background

gui(2).element_type = GUI_LABEL
gui(2).dialog = -1
GUI_init_element gui(2), "FTP Client, Version: " + VER$
gui(2).row1 = gui(1).row1 + 2
gui(2).col1 = _WIDTH(0) \ 2 - len(MEM_Get_str$(gui(2).nam)) \ 2

gui(3).element_type = GUI_LABEL
gui(3).dialog = -1
GUI_init_element gui(3), "Written by Matt Kilgore in QB64"
gui(3).row1 = gui(1).row1 + 4
gui(3).col1 = _WIDTH(0) \ 2 - len(MEM_Get_str$(gui(3).nam)) \ 2

gui(4).element_type = GUI_BUTTON
gui(4).dialog = -1
GUI_init_element gui(4), "Close"
gui(4).row1 = gui(1).row2 - 1
gui(4).col1 = gui(1).col1 + (gui(1).col2 - gui(1).col1) \ 2 - 2

exit_gui_flag = 0
selected_gui = 4

DO
  _LIMIT 100
  if GUI_update_screen(gui(), gui_num, selected_gui) then
    GUI_draw_element_array gui(), gui_num, selected_gui
  end if
  
  GUI_mouse_range gui(), gui_num, selected_gui
  
  key$ = GUI_inkey$(gui(), gui_num, selected_gui)
  
  if gui(4).pressed then exit_gui_flag = -1
  
LOOP until exit_gui_flag or key$ = CHR$(27)
GUI_free_element_array gui()
END SUB
